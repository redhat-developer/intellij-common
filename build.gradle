
plugins {
    id "org.jetbrains.intellij" version "1.5.2"
    id 'maven-publish'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

repositories {
    mavenLocal()
    mavenCentral()
}

apply plugin: 'idea'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'
apply plugin: 'jacoco'

intellij {
    version = ideaVersion //for a full list of IntelliJ IDEA releases please see https://www.jetbrains.com/intellij-repository/releases
    plugins = ['terminal', 'yaml']
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

java {
    withSourcesJar()
    withJavadocJar()
}

dependencies {
    compile (
        'io.fabric8:kubernetes-client:5.12.4',
        'io.fabric8:openshift-client:5.12.4',
        'org.apache.commons:commons-exec:1.3',
        'org.apache.commons:commons-lang3:3.12.0'
    )
    testImplementation(
        'org.assertj:assertj-core:3.17.1',
        'org.mockito:mockito-inline:3.7.0'
    )
}

task packageTests(type: Jar) {
    from sourceSets.test.output
    classifier = 'test'
}

group 'com.redhat.devtools.intellij'
version projectVersion // Plugin version


nexusPublishing {
    packageGroup = 'JBoss Releases Staging Profile'
    repositories {
        jbossNexus {
            nexusUrl = uri("https://repository.jboss.org/nexus/service/local/")
            snapshotRepositoryUrl = uri("https://repository.jboss.org/nexus/content/repositories/snapshots/")
            username = nexusUser // defaults to project.properties["myNexusUsername"]
            password = nexusPassword // defaults to project.properties["myNexusPassword"]
        }
    }
}

def testJarFile = file("$buildDir/libs/intellij-common-${projectVersion}-test.jar")
def testArtifact = artifacts.add('archives', testJarFile) {
    type 'test'
    classifier 'test'
    builtBy 'packageTests'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from project.components.java
            artifact testArtifact
            pom {
                name = 'IntelliJ Common'
                description = 'Common utilities for IntelliJ plugins'
                url = 'https://github.com/redhat-developer/intellij-common'
                licenses {
                    license {
                        name = 'Eclipse Public License 2.0'
                        url = 'https://www.eclipse.org/legal/epl-v20.html'
                    }
                }
                developers {
                    developer {
                        name = 'Red Hat Developer'
                        email = 'developers@redhat.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/redhat-developer/intellij-common.git'
                    developerConnection = 'scm:git:ssh://git@github.com:redhat-developer/intellij-common.git'
                    url = 'https://github.com/redhat-developer/intellij-common/'
                }
            }
        }
    }
}
