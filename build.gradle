
plugins {
    id "org.jetbrains.intellij" version "0.4.18"
    id 'maven-publish'
}

repositories {
    mavenLocal()
    mavenCentral()
}

apply plugin: 'idea'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'
apply plugin: 'jacoco'

intellij {
    version ideaVersion //for a full list of IntelliJ IDEA releases please see https://www.jetbrains.com/intellij-repository/releases
    plugins 'terminal'
}

dependencies {
    compile 'io.fabric8:kubernetes-client:5.0.0'
    compile 'org.apache.commons:commons-exec:1.3'
    testImplementation(
            'org.assertj:assertj-core:3.17.1',
            'org.mockito:mockito-inline:3.7.0'
    )
}

task packageTests(type: Jar) {
    from sourceSets.test.output
    classifier = 'test'
}

group 'com.redhat.devtools.intellij'
version projectVersion // Plugin version

def testJarFile = file("$buildDir/libs/intellij-common-${projectVersion}-test.jar")
def testArtifact = artifacts.add('archives', testJarFile) {
    type 'test'
    classifier 'test'
    builtBy 'packageTests'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from project.components.java
            artifact testArtifact
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://repository.jboss.org/nexus/content/groups/public/"
            def snapshotsRepoUrl = "https://repository.jboss.org/nexus/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = nexusUser
                password = nexusPassword
            }
        }
    }
}
