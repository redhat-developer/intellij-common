
plugins {
    id "org.jetbrains.intellij" version "0.4.18"
    id 'maven-publish'
    id 'io.codearte.nexus-staging' version "0.22.0"
}

repositories {
    mavenLocal()
    mavenCentral()
}

apply plugin: 'idea'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'io.codearte.nexus-staging'

intellij {
    version ideaVersion //for a full list of IntelliJ IDEA releases please see https://www.jetbrains.com/intellij-repository/releases
    plugins 'terminal'
}

java {
    withSourcesJar()
    withJavadocJar()
}

dependencies {
    compile 'io.fabric8:kubernetes-client:5.0.2'
    compile 'org.apache.commons:commons-exec:1.3'
    testImplementation(
            'org.assertj:assertj-core:3.17.1',
            'org.mockito:mockito-inline:3.7.0'
    )
}

task packageTests(type: Jar) {
    from sourceSets.test.output
    classifier = 'test'
}

group 'com.redhat.devtools.intellij'
version projectVersion // Plugin version


nexusStaging {
    serverUrl = 'https://repository.jboss.org/nexus/service/local/'
    username = nexusUser
    password = nexusPassword
    packageGroup = 'JBoss Releases Staging Profile'
}

def testJarFile = file("$buildDir/libs/intellij-common-${projectVersion}-test.jar")
def testArtifact = artifacts.add('archives', testJarFile) {
    type 'test'
    classifier 'test'
    builtBy 'packageTests'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from project.components.java
            artifact testArtifact
            pom {
                name = 'IntelliJ Common'
                description = 'Common utilities for IntelliJ plugins'
                url = 'https://github.com/redhat-developer/intellij-common'
                licenses {
                    license {
                        name = 'Eclipse Public License 2.0'
                        url = 'https://www.eclipse.org/legal/epl-v20.html'
                    }
                }
                developers {
                    developer {
                        name = 'Red Hat Developer'
                        email = 'developers@redhat.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/redhat-developer/intellij-common.git'
                    developerConnection = 'scm:git:ssh://git@github.com:redhat-developer/intellij-common.git'
                    url = 'https://github.com/redhat-developer/intellij-common/'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://repository.jboss.org/nexus/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://repository.jboss.org/nexus/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = nexusUser
                password = nexusPassword
            }
        }
    }
}
